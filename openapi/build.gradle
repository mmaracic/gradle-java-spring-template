//Home controller from this module has to be ignored bz top level application until

plugins {
    id 'java-library'
    id 'common-conventions'
    id "org.openapi.generator" version "7.3.0"
}

def props = new Properties()
file("../gradle.properties").withInputStream { props.load(it) }

repositories {
    mavenCentral()
}

dependencies {
    //used by generated controller code
    implementation("org.springframework.boot:spring-boot-starter-web")

    //openAPI
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:2.3.0"
    implementation "org.openapitools:jackson-databind-nullable:0.2.6"

}

openApiGenerate {
    generatorName = "spring"
    verbose = false
    validateSpec = true
    outputDir = "$rootDir/openapi"
    inputSpec = "$rootDir/openapi/spec/UserApi.yaml"
    skipOverwrite = false
    ignoreFileOverride = "$rootDir/openapi/.openapi-generator-ignore-initial" //specify user defined file that specifies files not to override in initial run i.e. to skip their generation
    apiPackage = "hr.mmaracic.api"
    modelPackage = "hr.mmaracic.api.model"
    configOptions = [
            delegatePattern: "false",
            dateLibrary: "java8",
            serializationLibrary: "jackson",
            configPackage: "hr.mmaracic.api.configuration",
            basePackage: "hr.mmaracic.api",
            interfaceOnly: "true", //should have same value as skipDefaultInterface, true when we implement Controller ourselves. Spring boot problems with controllers, skipping delegates, implementing ourselves
            skipDefaultInterface: "true",
            //useSpringController: "false", //does not work
            useSpringBoot3: "true"
    ]
}

compileJava.dependsOn tasks.openApiGenerate
processResources.dependsOn tasks.openApiGenerate
tasks.clean.delete.add(file("src"))